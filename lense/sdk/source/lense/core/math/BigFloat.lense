import lense.core.math.Real;
import lense.core.math.Float;
import lense.core.math.Whole;
import lense.core.math.Integer;
import lense.core.math.BigDecimal;
import lense.core.math.Rational;
import lense.core.lang.String;
import lense.core.math.Comparison;
import lense.core.lang.HashValue;
import lense.core.lang.Boolean;
import lense.core.lang.debug;
import lense.core.math.ComparisonException;
import lense.core.math.RealLineElement;

public value class BigFloat implements Float {


	private constructor (private numerator : Integer, private denominator : Integer);

	public constructor () {
		return new BigFloat(0,1);
	}

	public implicit constructor valueOf( other: Real) {
		if (other is Rational){
			return new BigFloat(other.numerator, other.denominator).simplify();
		} else if (other is Whole){
			return new BigFloat(other.asInteger(), new Int32.one());
		}
		return new BigFloat.parse(other.asString());
	}

	public implicit constructor valueOf( other: Whole){
		return new BigFloat(other.asInteger() , 1);
	}

	public implicit constructor valueOf( other: Float){
		if (other is BigFloat){
			return other;
		} else if (other.isNaN()) {
			return new BigFloat(0,0);
		} else if (other.isPositiveInfinity()) {
			return new BigFloat(1,0);
		} else if (other.isNegativeInfinity()) {
			return new BigFloat(-1,0);
		} else if (other.isNegativeZero()) {
			return new BigFloat(0,-1);
		} else {
			return new BigFloat.parse(other.asString());
		}
	}


	public constructor parse( representation : String){
		if (representation == "NaN"){
		   return new BigFloat(0,0);
		} else if (representation == "+Infinity"){
		   return new BigFloat(1,0);
		} else if (representation == "-Infinity"){
		   return new BigFloat(-1,0);
		}

		let pos : Maybe<Natural> = representation.indexOf(".");


		if (pos.isAbsent()){
			return new BigFloat(new Whole.parse(representation),1);
		} else {

			mutable let isNegative = false;
			mutable let positiveRepresentation = representation;
			if ( representation.starstWith("-")){
				positiveRepresentation = representation.subString(1);
				isNegative = true;
			}

			let len : Natural = pos.value; // this should not be necessary. 
			let power = positiveRepresentation.size &- len &- 1;		

			let wholePart = new Whole.parse(positiveRepresentation.removeAt(pos.value));

			mutable let value = new BigFloat(wholePart,10 ^^ power).simplify();

			if (isNegative){
				value = -value;
			}

			return value;
		}

	
	}

	public asFloat() => this;

	public override asString(): String {

		if (this.isNaN()){
			return "NaN";
		} else if (this.isPositiveInfinity()){
			return "+Infinity";
		} else if (this.isNegativeInfinity()){
			return "-Infinity";
		} else if (this.isNegativeZero()){
			return "-0";
		} else if (this.denominator == 1){
			return this.numerator.asString();
		} 
		return new BigDecimal( new Rational.fraction( this.numerator , this.denominator)).asString();

	} 

	public sign() : Integer {
		// sign of NaN is +  since its considered > +oo
		if (this.isNaN() || this.isPositiveInfinity()) {
			return 1;
		} else if (this.isNegativeInfinity() || this.isNegativeZero()) {
			return -1;
		} else if (this.isZero()) {
			return 0;
		}

		return this.numerator.sign();
	}

	public isNaN() => numerator == 0 && denominator == 0;

	public isNegativeZero() => numerator == 0 && denominator < 0;

	public isNegativeInfinity() => numerator < 0 && denominator == 0;

	public isPositiveInfinity() => numerator > 0 && denominator == 0;

	public isInfinity() => denominator == 0;
	
	public isPositive() => this.isNaN() || this.isPositiveInfinity() || numerator.isPositive();

	public isNegative() => this.isNegativeZero() || this.isNegativeInfinity() || numerator.isNegative();

	public isOne() => numerator == 1 && denominator != 0;

	public isZero() => numerator == 0 && denominator > 0;
	
	public symmetric() {
		if (this.isZero() || this.isNegativeZero()){
			return new BigFloat( this.numerator , -this.denominator);
		} 
			
		return new BigFloat( -this.numerator , this.denominator);
	}

    public abs() => this.isNegative() ? this.symmetric() : this;

	public override compareWith (other : RealLineElement) : Comparison {
		
		 if (other is BigFloat){

			if (this.isNaN()){
				if (other.isNaN()){
					return equal;
				} else {
					return greater;
				}
			} else if (this.isPositiveInfinity()){
				if (other.isNaN()){
					return smaller;
				} else if (other.isPositiveInfinity()) {
					return equal;
				} else {
					return greater;
				}
			} else if (this.isNegativeZero()){
				if (other.isNegativeZero()) {
					return equal;
				} else if (other.isNaN() || other.isPositiveInfinity() ||  other.isZero() || other.sign() > 0){
					return smaller;
				} else {
					return greater;
				}
			} else if (this.isZero()){
				if (other.isZero()) {
					return equal;
				} else if (other.isNaN() || other.isPositiveInfinity() || other.sign() > 0){
					return smaller;
				} else {
					return greater;
				}
			} else if (this.isNegativeInfinity()){
				if (other.isNegativeInfinity()) {
					return equal;
				} else {
					return smaller;
				}
			}
			
			let diff = this - other;
			
		    if (diff > 0){
				return greater;
			} else if (diff < 0){
				return smaller;
			}
	
		   return equal;
		} else if (other is RealLineElement){
		  return compareWith(new BigFloat.valueOf(other.asFloat()));
		}

		throw new ComparisonException();
	}  

	public override equalsTo(other: Any){
		return (other is RealLineElement) && this.compareWith(other).isEqual();
	} 

	public override hashValue() : HashValue {
		return numerator.hashValue().concat(denominator.hashValue());
	}



    public isWhole() : Boolean {
    	return this.numerator % this.denominator == 0;
    }
    
	public floor() : Float {
		if (this.isNaN() || this.isInfinity() || this.isNegativeZero()) {
    		return this;
    	} else if (this < 0){
			return -(-this).ceil();
		} else {
      		//extracted from http://www.cs.nott.ac.uk/~rcb/G51MPC/slides/NumberLogic.pdf
			return (this.numerator \ this.denominator).asFloat() ;
		}
	}

	public ceil() : Float {
		if (this.isNaN() || this.isInfinity() || this.isNegativeZero()) {
    		return this;
    	} else if (this < 0){
			return -(-this).floor();
		} else {
      		//extracted from http://www.cs.nott.ac.uk/~rcb/G51MPC/slides/NumberLogic.pdf
			return ((this.numerator + this.denominator - 1) \ this.denominator).asFloat();
		}
	}

	public round() : Float {
		if ( this.isNaN() || this.isNegativeZero() || this.isInfinity()){
			return this;
		}

		return this < 0 ? this.ceil() : this.floor();
	}

	public remainder( other: Float) : Float {
		if (this.isInfinity() && other.isInfinity() || this.isNaN() || other.isNaN()) {
			return new BigFloat(0,0); // NaN
		} else if (other.isZero()) {
			return new BigFloat(1,0).multiply(this.sign().asFloat());
		} else if (other.isNegativeZero()) {
			return new BigFloat(-1,0).multiply(this.sign().asFloat());
		} else if (other.isOne()) {
			return this;
		} 
		
		return this.minus(other.multiply(this.divide(other).round()));
	}

	public modulo(other: Float) : Float{
		if (this.isInfinity() && other.isInfinity() || this.isNaN() || other.isNaN()) {
			return new BigFloat(0,0); // NaN
		} else if (other.isZero()) {
			return new BigFloat(1,0).multiply(this.sign().asFloat());
		} else if (other.isNegativeZero()) {
			return new BigFloat(-1,0).multiply(this.sign().asFloat());
		} else if (other.isOne()) {
			return this;
		} 
		
		return this.minus(other.multiply(this.divide(other).floor()));
	}

	private simplify(): BigFloat {
		return simplify(this.numerator, this.denominator);
	} 

	private simplify( numerator : Integer,  denominator : Integer ) : BigFloat {
	
		mutable let normalizedNumerator = numerator;
		mutable let normalizedDenominator = denominator;

		if (denominator == 0){
			normalizedNumerator = normalizedNumerator.sign();
		} 
		if (numerator == 0){
			normalizedDenominator = normalizedDenominator.sign();
		}

		
		if (normalizedDenominator != 1 ){
			let gcd = normalizedNumerator.gcd(normalizedDenominator);
			
			normalizedNumerator = normalizedNumerator \ gcd;
            normalizedDenominator = normalizedDenominator \ gcd;
		}
		
		if (normalizedNumerator < 0){
			normalizedNumerator = -normalizedNumerator;
			normalizedDenominator = -normalizedDenominator;
		}

		return new BigFloat(normalizedNumerator , normalizedDenominator);
	
	}

	public plus (other: Float) : Float {

	     if (this.isNegativeZero() && other.isNegativeZero()){
			return this;
	     } else if ((this.isZero() || this.isNegativeZero() )   && (other.isZero() || other.isNegativeZero())) {
			return new BigFloat(0, 1);
		 } 

		 let r : BigFloat = other;

		 return simplify(
				 r.denominator * this.numerator + this.denominator * r.numerator , 
				 this.denominator * r.denominator 
		);

	}

	public minus (other: Float) : Float {

 		 if (this.isZero()) {
			if (other.isZero()){
				return this;
			}
			return other.symmetric();
		 } else if (other.isZero()){
			return this;
		 }

		 let r : BigFloat = other;

		 return simplify(
				 r.denominator * this.numerator - this.denominator * r.numerator , 
				 this.denominator * r.denominator 
		);

	}

	public multiply (other: Float) : Float {

		if (other.isOne()){
			return this;
		} else if (this.isNaN() 
				|| other.isNaN() 
				|| (this.isInfinity() && other.isInfinity())
				|| (this.isZero() && other.isZero())
		){
			return new BigFloat(0,0); // NaN
		} else if (this.isOne()){
			return other;
		} else if (this.isZero() || other.isZero()){
			return new BigFloat(0,1); // zero
		}

		 let r : BigFloat = other;

		 return simplify(
				 this.numerator * r.numerator , 
				 this.denominator * r.denominator 
		);

	}

	public divide (other: Float) : Float {

		if (other.isOne()){
			return this;
		} else if (this.isNaN() 
				|| other.isNaN() 
				|| (this.isInfinity() && other.isInfinity())
				|| (this.isZero() && other.isZero())
		){
			return new BigFloat(0,0); // NaN
		} else if (this.isOne()){
			return other.invert();
		} else if (this.isZero()){
			return new BigFloat(0,1);
		}

 		let r : BigFloat = other;

		 return simplify(
				 this.numerator * r.denominator , 
				 this.denominator * r.numerator 
		);

	}

	public invert() : Float {
		if (this.isNaN()){
			return new BigFloat(0,0);
		} else if (this.isNegativeInfinity()) {
			return new BigFloat(0,-1);
		} else if (this.isPositiveInfinity()) {
			return new BigFloat(0,1);
		} else if (this.isOne()){
			return this;
		} else if (this.isZero()){
			return new BigFloat(1,0);
		}

		return new BigFloat(this.denominator, this.numerator);
	}

	public log(): Float {
		if (this.isNaN() || this <= 0 || this.isNegativeInfinity() ){
			return new BigFloat(0,0);
		} else if (this.isPositiveInfinity()) {
			return new BigFloat(1,0);
		} else if (this.isOne()){
			return new BigFloat(0,1);
		}
			
		return this.numerator.log() - this.denominator.log();
		
	}

	public exp(): Float{

		if (this.isZero() || this.isNegativeZero() ){
			return new BigFloat(1 , 1);
		} else 	if (this.isNegativeInfinity()){
			return new BigFloat(0 , 1);
		} else 	if (this.isPositiveInfinity()){
			return new BigFloat(1 , 0);
		} else 	if (this.isNaN()){
			return new BigFloat(0 , 0);
		}

		if (this < 0){
			return this.abs().exp().invert();
		}


	    //taylor : exp(x) = 1 + x + x*x/2 + x*x*X/6 + x*x*x*x/24 +...

        mutable let sum : BigFloat = 1 ;
   		mutable let xpowi : BigFloat = 1 ;
		mutable let factorial = 1;
                           
         for(let i in 1 .. 36){
			factorial *= i;
			xpowi *= this;
			let c = xpowi / factorial;
			sum += c;
 			if ( xpowi.abs() < i && c.abs() < 0.5 ){
  				break;
 			}                               
         }

		return sum;

	}
	
	public raiseTo(other : Float) : Float {
		return (this.log() * other).exp();
	}

	public raiseTo(other : Whole) : Float {


		let n = other.abs();

		mutable let power =  simplify(this.numerator ^^ n, this.denominator ^^ n);
	
		if (other < 0 ){
			power =  power.invert();
		}

		return power;
	}
}