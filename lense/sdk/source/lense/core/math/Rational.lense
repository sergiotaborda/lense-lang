
import lense.core.math.Real;
import lense.core.math.Integer;
import lense.core.math.Natural;
import lense.core.lang.String;
import lense.core.lang.Boolean;
import lense.core.math.ArithmeticException;
import lense.core.math.Greater;
import lense.core.math.Smaller;
import lense.core.math.Equal;
import lense.core.math.BigDecimal;
import lense.core.math.equal;
import lense.core.math.Comparable;

/{
	A fraction n/d 
}/
public value class Rational implements Real {

	private constructor ( public  numerator : Integer , public denominator : Natural);

	public constructor zero() {
		return new Rational(0 , 1);
	}

	public constructor one() {
		return new Rational(1 , 1);
	}

	public implicit constructor valueOf(other : Integer) {
		return new Rational(other , 1);
	}

	public implicit constructor valueOf(other : Whole) {
		return other.divide(1);
	}

	public constructor (  numerator : Integer ,   denominator : Integer ){
	
		if (denominator == 0){
			throw new ArithmeticException("Cannot create a Rational with zero as denominator"); 
		}
		
		if (numerator == 0){
			// all forms of zero are the same. 
			denominator = 1;
		} else if (denominator < 0){ // let the numerator carrie the sign
			numerator = -numerator;
			denominator = -denominator;
		}
		
		if (denominator != 1 ){
			let gcd = numerator.gcd(denominator);
			numerator = numerator \ gcd;
            denominator = denominator \ gcd;
		}
		
		return new Rational(numerator , denominator.abs());
	}
	
	private simplify( numerator : Integer,  denominator : Integer ) : Rational {
			return simplify(numerator * denominator.sign() , denominator.abs()) ;
	}

	private simplify( numerator : Integer,  denominator : Natural ) : Rational {
	
		if (numerator == 0){
			// all forms of zero are the same. 
			denominator = 1;
		} 
		
		if (denominator != 1 ){
			let gcd = numerator.gcd(denominator);
			
			// TODO log (gcd)
			numerator = numerator \ gcd;
            denominator = denominator \ gcd;
		}
		
		return new Rational(numerator , denominator);
	
	}
	
	public override equalsTo(other : Any) : Boolean {

		if ( other is Whole && this.denominator == 1){
			return this.numerator == other;
		} else if(other is Rational){
			return this.numerator == other.numerator 
				&& this.denominator == other.denominator;
		} else if(other is Real){
			return this.compareWith(other).isEqual();
		} else if (other is Number && other is Comparable){
			return (this <=> other) == equal;
		}
		
		
		// otherwise is not a comparable number
		return false; 
	}

	public override hashValue() : HashValue {
		return numerator.hashValue().concat(denominator.hashValue());
	}
	
	public override asString() : String {
		if (this.denominator == 1){
			return this.numerator.asString();
		} 
		return this.numerator.asString() ++ "/" ++ this.denominator.asString();
	}
	
	public plus( other : Real) : Real {
		if (other is Rational){
			return plus(other);
		} 
		
		return promoteNext() + other;
		
	}
	
	private  promoteNext() : Real {
		return promoteToBigDecimal(); 
	}
	
	protected promoteToBigDecimal() : BigDecimal {
		return new BigDecimal(this);
	}

	public plus( other : Rational) : Rational {
		 return simplify(
				 other.denominator * this.numerator   + this.denominator * other.numerator , 
				 this.denominator * other.denominator 
		 );
	}

	public minus( other : Real) : Real {
		if (other is Rational){
			return minus(other);
		} 
		
		return promoteNext() - other;
	}
	
	public minus( other : Rational) : Rational{
		 return simplify(
				 other.denominator * this.numerator  - this.denominator * other.numerator , 
				 this.denominator * other.denominator 
		 );
	}

	public  multiply(other : Real) : Real {
		if (other is Rational){
			return multiply(other); // TODO flow senstivie cast
		} 
			
		return promoteNext() * other;
		
	}
	
	public multiply( other : Rational) : Rational {
		 return simplify(
				 this.numerator * other.numerator,
				 this.denominator * other.denominator
		 );
	}

	public divide( other : Real) : Real {
		if (other is Rational){
			return divide(other);
		} 
		
		return promoteNext().divide(other);
		
	}

	public divide( other: Rational) : Rational {
		 if (other == 0){
			throw new ArithmeticException("Cannot divide by zero"); 
		}
		
		var n = other.denominator * this.numerator;
		if (other.numerator < 0){ // set the sign in the final numerator 
			n = -n;
		}
		return simplify(
				 n,
				 this.denominator * other.numerator.abs()
		);
	}
	
	public isZero() : Boolean {
		return this.numerator.isZero();
	}

	public isOne() : Boolean{
		return this.numerator == this.denominator;
	}

	public isNegative() : Boolean{
		return sign() == -1;
	}

	public isPositive() : Boolean{
		return sign() == 1;
	}

	public symmetric() : Real {
		return new Rational(numerator.symmetric(), denominator);
	}
	
	public sign() : Integer {
		return numerator.sign();
	}

    public isWhole() : Boolean {
    	return this.numerator % this.denominator == 0;
    }
    
	public floor() : Integer {
		if (this < 0){
			return -(-this).ceil();
		} else {
      		//extracted from http://www.cs.nott.ac.uk/~rcb/G51MPC/slides/NumberLogic.pdf
			return this.numerator \ this.denominator ;
		}
	}

	public ceil() : Integer {

		if (this < 0){
			return -(-this).floor();
		} else {
      		//extracted from http://www.cs.nott.ac.uk/~rcb/G51MPC/slides/NumberLogic.pdf
			return (this.numerator + this.denominator - 1) \ this.denominator;
		}
	}



    public raiseTo(exponent : Real) : Real {
    	if (this == 0){ 
    		if (exponent == 0){
    			return new Rational(1,1);
    		}
            return this;
        } else if ( this == 1) {
        	return this;
        } else if ( exponent == 0) {
        	return new Rational(1,1);
        } else if (exponent.isWhole()){
            let p : Integer = exponent.floor();
            let n : Natural = p.abs();
            var result : Rational = new Rational(this.numerator ^^ n, this.denominator ^^ n);
            if (p < 0) {
                result = result.invert();
            } 
            return result;
        } else {
        	let n = this.numerator ^^ exponent;
        	let d = this.denominator ^^ exponent;
        	
        	if (n.isWhole() && d.isWhole()){
        		return new Rational( n.floor() , d.floor());
        	}

            return n/ d;
        }
    }

	public invert () : Rational {
		if (this.isZero()){
            throw new ArithmeticException("Cannot invert zero");
        }

		let sign = this.numerator.sign();
		return new Rational( this.denominator * sign, this.numerator.abs());
	}
	
	public abs() : Real {
	  var n = this.numerator;
	  if (n < 0 ){
	  	n = -n;
	  }
	  
	  return new Rational(n, this.denominator);
    }

	public override compareWith (other : Real) : Comparison {
		
		let diff = this - other;
		
	    if (diff >0){
			return greater;
		} else if (diff <0){
			return smaller;
		}

	   return equal;

	}  

	public asDecimal() : BigDecimal {
		return new BigDecimal(this);
	} 

	public plus( other : Imaginary) : Complex {
		 return new Complex.retangular(this, other.real());
	}

	public minus( other : Imaginary) : Complex {
		return new Complex.retangular(this, -other.real());		 
	}

	public multiply( other : Imaginary) : Imaginary {
		 return new Imaginary.valueOf(this * other.real());
	}

	public divide( other : Imaginary) : Imaginary {
		return new Imaginary.valueOf(this / other.real());
	}
}